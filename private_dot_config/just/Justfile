# Justfile — Bluefin baseline for MacBookPro11,5 (Haswell/Iris Pro)
# Usage:
#   just --list
#   just baseline
#   just vscode-disable-hwaccel
#
# Notes:
# - Many tasks affect the next boot (rpm-ostree kargs).
# - kargs changes create a new deployment. Pin known-good deployments.

set shell := ["bash", "-eu", "-o", "pipefail", "-c"]

HOME := env_var("HOME")
USER := env_var("USER")

# --- Helpers ---------------------------------------------------------------

@_default:
  just --list

@_need cmd extramsg="":
  command -v "{{cmd}}" >/dev/null 2>&1 || { echo "### Missing required command: {{cmd}}"; if [ -n "### {{extramsg}}" ]; then echo "{{extramsg}}"; fi; exit 1; }

@_is_bluefin:
  grep -q '^ID=bluefin' /etc/os-release || { echo "This doesn't look like Bluefin (ID!=bluefin). Aborting."; exit 1; }

@_json_set_bool file key value:
  just _need jq
  { \
    mkdir -p "$(dirname "{{file}}")"; \
    tmp="$(mktemp)"; \
    if [ -f "{{file}}" ] && jq -e . "{{file}}" >/dev/null 2>&1; then \
      jq --arg k "{{key}}" --argjson v {{value}} '. + {($k): $v}' "{{file}}" > "$tmp"; \
    else \
      echo '{}' | jq --arg k "{{key}}" --argjson v {{value}} '. + {($k): $v}' > "$tmp"; \
    fi; \
    mv "$tmp" "{{file}}"; \
  }

# Clone a git repo if missing; skip if correct repo already present.
# Usage: just _clone_repo <dest> <url>
_clone_repo dest url:
    #!/usr/bin/env bash
    set -euo pipefail

    dest="{{dest}}"
    url="{{url}}"

    # Accept both .git and non-.git origin URLs as equivalent
    url_nogit="${url%.git}"

    if [ -d "$dest/.git" ]; then
        origin="$(git -C "$dest" remote get-url origin 2>/dev/null || true)"

        if [[ "$origin" == "$url" || "$origin" == "$url_nogit" ]]; then
            echo "### $dest already contains correct repo; skipping."
            exit 0
        fi

        echo "### ERROR: $dest is a git repo but origin is:"
        echo "###   $origin"
        echo "### Expected:"
        echo "###   $url"
        exit 1
    fi

    if [ -e "$dest" ]; then
        echo "### ERROR: $dest exists but is not a git repository."
        echo "### Refusing to overwrite."
        exit 1
    fi

    echo "### Cloning $url -> $dest"
    git clone "$url" "$dest"

@_ensure_rpms pkgs:
  @if ! rpm -q {{pkgs}} >/dev/null 2>&1; then \
    sudo rpm-ostree install {{pkgs}}; \
  fi

[no-exit-message]
@_package_installed pkg:
    rpm-ostree status --json | jq -e '.deployments[0].packages | index("{{pkg}}")' >/dev/null

# --- Info / sanity ---------------------------------------------------------

# Show current configuration
@status:
  just _is_bluefin
  @### OS ###
  cat /etc/os-release | sed -n '1,12p'
  echo
  @### Session ###
  echo "XDG_SESSION_TYPE=${XDG_SESSION_TYPE:-unknown}"
  echo
  @### Full kernel cmdline ###
  cat /proc/cmdline
  echo
  @### Kernel cmdline (i915) ###
  cat /proc/cmdline | tr ' ' '\n' | grep -E '^i915\.' || true
  echo
  @### rpm-ostree status ###
  rpm-ostree status

# --- Hardware-specific config -----------------------------------------

# Append the i915 safety flags you’ve been using.
i915-append:
  @just _is_bluefin
  @sudo rpm-ostree kargs \
    --append-if-missing=i915.enable_psr=0 \
    --append-if-missing=i915.enable_fbc=0 \
    --append-if-missing=i915.enable_dc=0 \
    --append-if-missing=i915.fastboot=0
  ### Added i915 kernel args (if there were changes, they will take effect after reboot)."

# Remove those i915 flags again (for testing).
i915-remove:
  @just _is_bluefin
  sudo rpm-ostree kargs \
    --delete=i915.enable_psr \
    --delete=i915.enable_fbc \
    --delete=i915.enable_dc \
    --delete=i915.fastboot
  @echo "Removed i915 kernel args (takes effect after reboot)."

# Optional “max compatibility” additions (only if you hit new-kernel freezes/regressions).
i915-append-max-compat:
  @just _is_bluefin
  sudo rpm-ostree kargs \
    --append-if-missing=i915.enable_guc=0 \
    --append-if-missing=i915.enable_huc=0 \
    --append-if-missing=i915.mitigations=off
  @echo "Added max-compat i915 args (if there were changes, they will take effect after reboot)."

# Disable idle-triggered suspend (MBP11,5 only)
disable-idle-suspend:
    #!/usr/bin/env bash
    set -euo pipefail

    # Guard: only apply to MacBookPro11,5
    if [[ "$(cat /sys/class/dmi/id/sys_vendor 2>/dev/null)" != "Apple Inc." ]] || \
       [[ "$(cat /sys/class/dmi/id/product_name 2>/dev/null)" != "MacBookPro11,5" ]]; then
      echo "### Not MacBookPro11,5; skipping idle-suspend workaround."
      exit 0
    fi

    conf_dir="/etc/systemd/logind.conf.d"
    conf_file="${conf_dir}/99-disable-idle-suspend.conf"
    desired_content=$'[Login]\nIdleAction=ignore\n'

    echo "### Disabling idle suspend on MacBookPro11,5"

    tmp="$(mktemp)"
    trap 'rm -f "$tmp"' EXIT
    printf '%s' "$desired_content" >"$tmp"

    sudo install -d -m 0755 "$conf_dir"

    if sudo test -f "$conf_file" && sudo cmp -s "$tmp" "$conf_file"; then
      echo "### Already configured; nothing to do."
      exit 0
    fi

    sudo install -m 0644 "$tmp" "$conf_file"

    echo "### Restarting systemd-logind"
    sudo systemctl restart systemd-logind

    echo "### Verified config:"
    systemd-analyze cat-config systemd/logind.conf \
      | sed -n '/99-disable-idle-suspend\.conf/,+5p' || true

# Status of idle suspend configuration
@idle-suspend-status:
    systemd-analyze cat-config systemd/logind.conf | grep -nE 'IdleAction|99-disable-idle-suspend\.conf' || true

# --- Deployment hygiene ----------------------------------------------------

# Pin the currently booted deployment so it won't be GC’d.
pin-current:
  @just _need ostree
  @sudo ostree admin pin 0
  @echo "Pinned current deployment (index 0)."

# Show deployments with indices (useful before pin/undeploy decisions).
deployments:
  @just _need ostree
  @sudo ostree admin status

# --- Electron / VS Code stability -----------------------------------------

# VS Code: disable hardware acceleration (JSON-aware merge) for your installation path.
vscode-disable-hwaccel:
  @just _json_set_bool "{{HOME}}/.vscode/argv.json" "disable-hardware-acceleration" true
  @echo "Wrote/merged {{HOME}}/.vscode/argv.json: disable-hardware-acceleration=true"

# VS Code: re-enable hardware acceleration (JSON-aware merge).
vscode-enable-hwaccel:
  @just _json_set_bool "{{HOME}}/.vscode/argv.json" "disable-hardware-acceleration" false
  @echo "Wrote/merged {{HOME}}/.vscode/argv.json: disable-hardware-acceleration=false"

# A generic Electron flags file (some Electron apps honor this; VS Code often does not).
electron-global-disable-gpu:
  @mkdir -p "{{HOME}}/.config"
  @( echo "--disable-gpu"; echo "--disable-gpu-compositing" ) > "{{HOME}}/.config/electron-flags.conf"
  ### Wrote {{HOME}}/.config/electron-flags.conf (some Electron apps may honor this)." ###

# --- Package installation -------------------------------------------------

### 1Password
### Instructions from https://monospacementor.com/wiki/Notes/Install+1Password+on+Fedora+Silverblue with some improvements.

# Install 1Password using the official RPM repo (Fedora Silverblue / Bluefin)
onepassword-install:
    #!/usr/bin/env bash

    if just _package_installed 1password; then
      echo "### 1Password already layered; nothing to do."
      exit 0
    fi
    
    just _need curl || exit 1
    echo "### ==> Installing 1Password GPG key"
    sudo install -d -m 0755 /etc/pki/rpm-gpg
    curl -fsSL https://downloads.1password.com/linux/keys/1password.asc | sudo tee /etc/pki/rpm-gpg/RPM-GPG-KEY-1password >/dev/null

    echo "### ==> Installing official 1Password repo"
    (echo "[1password]"; \
    echo "name=1Password Stable Channel"; \
    echo "baseurl=https://downloads.1password.com/linux/rpm/stable/\$basearch"; \
    echo "enabled=1"; \
    echo "gpgcheck=1"; \
    echo "repo_gpgcheck=1"; \
    echo "gpgkey=https://downloads.1password.com/linux/keys/1password.asc") | sudo tee /etc/yum.repos.d/1password.repo

    echo "### ==> Layering 1Password packages (activation requires reboot)"
    just _ensure_rpms "1password 1password-cli"

    echo
    echo "### 1Password installed into next deployment."
    echo "### Reboot to activate: systemctl reboot"

# Disable GPU acceleration for 1Password (Electron stability on Haswell)
onepassword-disable-hwaccel:
  @just _json_set_bool "{{HOME}}/.config/1Password/settings/settings.json" "app.useHardwareAcceleration" false
  ### 1Password GPU acceleration disabled (settings.json updated)

# Verify 1Password installation
onepassword-status:
    # == Repo file ==
    @test -f /etc/yum.repos.d/1password.repo && echo "✓ repo present" || echo "✗ repo missing"
    @echo
    # == Packages in deployment ==
    @rpm -q 1password 1password-cli 2>/dev/null || echo "Not yet active (reboot pending?)"
    @echo 
    # == Hardware acceleration ==
    @test -f "{{HOME}}/.config/1Password/settings/settings.json" && jq -e '.["app.useHardwareAcceleration"] == false' "{{HOME}}/.config/1Password/settings/settings.json" >/dev/null 2>&1 && echo "✓ hardware acceleration disabled" || echo "✗ hardware acceleration enabled or unset"

### Ghostty
### Instructions from https://ghostty.org/docs/install/binary#atomic-desktops-(silverblue)

# Install the Ghostty terminal emulator
ghostty-install:
    #!/usr/bin/env bash

    if just _package_installed ghostty; then
      echo "### Ghostty already layered; nothing to do."
      exit 0
    fi
    
    echo "### ==> Installing official Ghostty repo"
    . /etc/os-release && curl -fsSL "https://copr.fedorainfracloud.org/coprs/scottames/ghostty/repo/fedora-${VERSION_ID}/scottames-ghostty-fedora-${VERSION_ID}.repo" | sudo tee /etc/yum.repos.d/_copr:copr.fedorainfracloud.org:scottames:ghostty.repo > /dev/null
    echo "@### ==> Layering Ghostty package (activation requires reboot)"
    just _ensure_rpms ghostty
    echo
    echo "### Ghostty installed into next deployment."
    echo "### Reboot to activate: systemctl reboot"

# Install GUI Emacs and helpers (idempotent)
emacs-install:
    #!/usr/bin/env bash
    if just _package_installed emacs; then
      echo "### Emacs already layered; nothing to do."
      exit 0
    fi

    echo "### Installing Emacs (GUI) and support packages"
    just _ensure_rpms "emacs ripgrep fd-find aspell"; 
    echo "### Packages installed; reboot required to activate."; 

# Install or update Doom Emacs safely
doom-emacs-install:
  ### Installing Doom Emacs
  @just _need git

  #   If ~/.emacs.d exists but is not Doom, back it up
  @{ \
    if [ -d "{{HOME}}/.emacs.d" ] && \
       [ ! -f "{{HOME}}/.emacs.d/bin/doom" ] && \
       [ ! -f "{{HOME}}/.emacs.d/core/core.el" ]; then \
      backup="{{HOME}}/.emacs.d.backup.$$(date +%Y%m%d-%H%M%S)"; \
      echo "Backing up existing ~/.emacs.d to $$backup"; \
      mv "{{HOME}}/.emacs.d" "$$backup"; \
    fi; \
  }

  #   Install Doom Emacs if missing
  @{ \
    if [ ! -d "{{HOME}}/.emacs.d" ]; then \
      git clone --depth 1 https://github.com/doomemacs/doomemacs "{{HOME}}/.emacs.d"; \
    fi; \
  }

  #   Run Doom install or update
  @{ \
    if [ -x "{{HOME}}/.emacs.d/bin/doom" ]; then \
      if [ ! -d "{{HOME}}/.doom.d" ]; then \
        just clone-emacs-config; \
        "{{HOME}}/.emacs.d/bin/doom" install; \
      else \
        "{{HOME}}/.emacs.d/bin/doom" upgrade; \
      fi; \
    else \
      echo "ERROR: Doom binary not found after install"; \
      exit 1; \
    fi; \
  }

# --- Git Credential Manager

gcm_version := "2.6.1"
gcm_arch    := "linux_amd64"
gcm_bindir  := "/usr/local/bin"

# Install Git Credential Manager (GCM) from official tarball
git-cred-install:
  #!/usr/bin/env bash
  set -euo pipefail

  gcm_url="https://github.com/git-ecosystem/git-credential-manager/releases/download/v{{gcm_version}}/gcm-{{gcm_arch}}.{{gcm_version}}.tar.gz"
  gcm_bin="{{gcm_bindir}}/git-credential-manager"

  if [ -x "$gcm_bin" ]; then
    echo "### git-credential-manager already installed"
    "$gcm_bin" --version || true
    exit 0
  fi

  tmpdir="$(mktemp -d)"
  trap 'rm -rf "$tmpdir"' EXIT

  archive="$tmpdir/gcm.tar.gz"

  echo "### Downloading GCM {{gcm_version}}"
  curl -L --fail -o "$archive" "$gcm_url"

  echo "### Extracting"
  tar -xzf "$archive" -C "$tmpdir"

  if [ ! -f "$tmpdir/git-credential-manager" ]; then
    echo "### ERROR: git-credential-manager binary not found in archive"
    exit 1
  fi

  echo "### Installing to {{gcm_bindir}}"
  sudo install -m 0755 "$tmpdir/git-credential-manager" "$gcm_bin"

  echo "### Installed:"
  "$gcm_bin" --version

# Configure Git to use GCM with Secret Service
@git-cred-configure:
    just _need git-credential-manager "run: just git-cred-install"

    @### Configuring GCM
    git-credential-manager configure

    @### Setting credential store to Secret Service
    git config --global credential.credentialStore secretservice

    @### Setting recommended defaults
    git config --global credential.useHttpPath true

    just git-cred-status

# Show Git credential helper configuration
@git-cred-status:
    @### git-credential-manager
    command -v git-credential-manager
    git-credential-manager --version

    @### git credential config (global)
    git config --global --show-origin --show-names --get-all credential.helper || true
    git config --global --show-origin --show-names --get credential.credentialStore || true
    git config --global --show-origin --show-names --get credential.useHttpPath || true

# --- Fonts management -------------------------------------------------------

### Fonts policy:
### - Large / upstream fonts (e.g. Nerd Fonts) → manifest.tsv + fonts-install
### - Small, stable, personal fonts (e.g. Alegreya Variable) → chezmoi

# manifest in format name<TAB>url<TAB>sha256
fonts_manifest := env_var_or_default("FONTS_MANIFEST", env_var("HOME") + "/.config/fonts/manifest.tsv")
fonts_cache    := env_var("HOME") + "/.cache/fonts/archives"
fonts_dir      := env_var("HOME") + "/.local/share/fonts"
fonts_staging  := env_var("HOME") + "/.cache/fonts/staging"

# Install fonts listed in ~/.config/fonts/manifest.tsv
fonts-install:
    #!/usr/bin/env bash
    ### Install fonts listed in the manifest (download, verify, extract)
    set -euo pipefail
    mkdir -p "{{fonts_cache}}" "{{fonts_dir}}" "{{fonts_staging}}"

    changed=0

    # Read: name \t url \t sha256
    while IFS=$'\t' read -r name url sha; do
      [ -z "${name:-}" ] && continue
      case "$name" in \#* ) continue ;; esac

      archive="{{fonts_cache}}/$name"

      ### Download if missing
      if [ ! -f "$archive" ]; then
        echo "### Downloading $name"
        curl -L --fail -o "$archive" "$url"
      else
        echo "### Using cached $name"
      fi

      ### Verify checksum (if provided)
      if [ -n "${sha:-}" ] && [ "$sha" != "-" ]; then
        echo "### Verifying $name"
        echo "$sha  $archive" | sha256sum -c -
      else
        echo "### Skipping checksum for $name"
      fi

      ### Extract into a clean staging dir
      tmpdir="{{fonts_staging}}/${name}.d"
      rm -rf "$tmpdir"
      mkdir -p "$tmpdir"
      echo "### Extracting $name"
      unzip -q "$archive" -d "$tmpdir"

      ### Compute a bundle fingerprint from extracted font files
      # (Stable ordering, includes file names + content via sha256sum output)
      bundle_fp="$(
        find "$tmpdir" -type f \( -iname '*.ttf' -o -iname '*.otf' -o -iname '*.ttc' \) -print0 \
          | LC_ALL=C sort -z \
          | xargs -0 sha256sum \
          | sha256sum \
          | awk '{print $1}'
      )"

      ### Determine destination dir from archive name
      base="$name"
      base="${base%.tar.gz}"; base="${base%.zip}"; base="${base%.tar}"
      destdir="{{fonts_dir}}/$base"
      stamp="$destdir/.installed.fp"

      ### Skip if already installed and fingerprint matches
      if [ -f "$stamp" ] && [ "$(cat "$stamp")" = "$bundle_fp" ]; then
        echo "### $base already installed (fingerprint match); skipping."
        continue
      fi

      echo "### Installing fonts from $name into $destdir"
      mkdir -p "$destdir"

      # Install/overwrite font files (idempotent)
      find "$tmpdir" -type f \( -iname '*.ttf' -o -iname '*.otf' -o -iname '*.ttc' \) -print0 \
        | while IFS= read -r -d '' f; do
            install -m 0644 "$f" "$destdir/"
          done

      echo "$bundle_fp" > "$stamp"
      changed=1

    done < "{{fonts_manifest}}"

    ### Rebuild font cache only if something changed
    if [ "$changed" -eq 1 ]; then
      echo "### Rebuilding font cache"
      fc-cache -f "{{fonts_dir}}"
    else
      echo "### No font changes; skipping fc-cache."
    fi

# List installed user fonts
fonts-list:
    fc-list | grep -F "{{fonts_dir}}" || true

# Remove downloaded archives and staging
fonts-clean-cache:
    rm -rf "{{fonts_cache}}" "{{fonts_staging}}"
    
# --- Config and dotfiles ---------------------------------------------------

# Clone Elvish configuration
@clone-elvish-config:
  @### Cloning Elvish config
  just _clone_repo ~/.config/elvish https://github.com/zzamboni/dot-elvish.git 

# Commit Elvish configuration
@commit-elvish-config:
  @### Committing Elvish config
  cd ~/.config/elvish; git commit -a -m 'Latest changes' || true; git push || true

# Clone Doom Emacs configuration
@clone-emacs-config:
  @### Cloning Emacs config
  just _clone_repo ~/.doom.d https://github.com/zzamboni/dot-doom.git

# Create useful symlinks
@symlink-dirs:
    @### Setting up symlinks
    ln -sf ~/Dropbox/Personal ~
    ln -sf ~/Dropbox/Reference ~
    ln -sf ~/Dropbox/bin ~

# --- Baseline meta-task ----------------------------------------------------

# Run the baseline you likely want on MBP11,5
[doc('Set minimum baseline for MBP11,5')]
@baseline:
    @### Kernel and hardware config
    just i915-append
    just disable-idle-suspend
    @### Installing and configuring apps
    just vscode-disable-hwaccel
    just onepassword-install
    just onepassword-disable-hwaccel
    just ghostty-install
    just fonts-install
    just emacs-install
    @### Cloning configs
    just clone-elvish-config
    just clone-emacs-config
    just doom-emacs-install
    just symlink-dirs
    just git-cred-install
    just git-cred-configure
    echo
    @### Baseline applied. Next step: reboot to activate changes (not needed if no changes reported above)
    @###    systemctl reboot
